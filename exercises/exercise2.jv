pipeline NeussTreeDataPipeline {
    block CSVDataFetch oftype HttpExtractor {
        url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/stadt-neuss-herbstpflanzung-2023/exports/csv";
    }

    block RawTextInterpreter oftype TextFileInterpreter {}

    block DelimitedDataParser oftype CSVInterpreter {
        enclosing: '';
        delimiter: ";";
    }

    block RemoveGermanNameColumn oftype ColumnDeleter {
        delete: [column E];
    }
    constraint FurthAreaConstraint oftype RegexConstraint {
        regex: /^Furth-.*/;
    }
    constraint CoordinateFormatConstraint oftype RegexConstraint {
        regex: /^\d{1,3}\.\d+,\s*\d{1,3}\.\d+$/;
    }

    valuetype FurthStadtteil oftype text {
        constraints: [FurthAreaConstraint];
    }

    valuetype Geocoordinate oftype text {
        constraints: [CoordinateFormatConstraint];
    }

    block DataInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "lfd_nr" oftype integer,
            "stadtteil" oftype FurthStadtteil,
            "standort" oftype text,
            "baumart_botanisch" oftype text,
            "id" oftype Geocoordinate,
            "baumfamilie" oftype text,
        ];
    }

    block TreeDataToSQLite oftype SQLiteLoader {
        table: "trees";
        file: "./trees.sqlite";
    }

    CSVDataFetch 
         -> RawTextInterpreter
         -> DelimitedDataParser
         -> RemoveGermanNameColumn
         -> DataInterpreter
         -> TreeDataToSQLite;
}
